' Pumpkin Spice Example 3
' WHILE statements

' A FOR loop is nice, but it always repeats a fixed number of times

' Sometimes you want to loop until a condition is true
' Here's a simple game

PLAYING = "Y"

' The game repeats as long as PLAYING has the text "Y"
WHILE PLAYING = "Y"
  ATTEMPT = 0

  ' RANDOM is a built in function that returns a random number
  ' in a given range

  MY_NUMBER = RANDOM(1,10)
  GUESS = ""

  SAY "I'm thinking of a number 1 to 10"
  SAY "What is it?"

  ' The user keeps guessing until they get it right
  WHILE NUMBER(GUESS) != MY_NUMBER
    ATTEMPT = ATTEMPT + 1
    LISTEN FOR GUESS

    IF NUMBER(GUESS) < MY_NUMBER
      SAY "Higher"
    END IF
    IF NUMBER(GUESS) > MY_NUMBER
      SAY "Lower"
    END IF
  END WHILE

  SAY "You got it!"
  SAY "It only took you "+TEXT(ATTEMPT)+" tries"
  SAY
  SAY "Type ""Y"" to play again";
  LISTEN FOR PLAYING
END WHILE

SAY "Reload the page to play again"