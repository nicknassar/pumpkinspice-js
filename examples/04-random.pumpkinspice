' Pumpkin Spice example 4
' RANDOM

' In the last example, we introduced the
' RANDOM(low, high) function for generating
' random numbers. Sometimes you want to choose
' an option randomly

' This program is an infinite loop
' It just keeps asking for a question
WHILE 1 = 1 DO

  ' Wait for the user to enter a question
  ' Keep asking if the input is blank
  QUESTION = ""
  WHILE QUESTION = ""
    SAY "Ask the Magic 8-Ball a ""Yes or No"" question:"
    LISTEN FOR QUESTION
  END WHILE

  SAY "The Magic 8-Ball says";
  SLEEP 400
  SAY ".";
  SLEEP 400
  SAY ".";
  SLEEP 400
  SAY "."

  ' Each WITH CHANCE section has an equal chance of being run
  BEGIN RANDOM
  WITH CHANCE
    SAY "It is certain"
  WITH CHANCE
    SAY "Without a doubt"
  WITH CHANCE
    SAY "You may rely on it"
  WITH CHANCE
    SAY "Yes definitely"
  WITH CHANCE
    SAY "It is decidedly so"
  WITH CHANCE
    SAY "As I see it, yes"
  WITH CHANCE
    SAY "Most likely"
  WITH CHANCE
    SAY "Yes"
  WITH CHANCE
    SAY "Outlook good"
  WITH CHANCE
    SAY "Signs point to yes"

  WITH CHANCE
    SAY "Reply hazy try again"
  WITH CHANCE
    SAY "Better not tell you now"
  WITH CHANCE
    SAY "Ask again later"
  WITH CHANCE
    SAY "Cannot predict now"
  WITH CHANCE
    SAY "Concentrate and ask again"

  WITH CHANCE
    SAY "Donâ€™t count on it"
  WITH CHANCE
    SAY "Outlook not so good"
  WITH CHANCE
    SAY "My sources say no"
  WITH CHANCE
    SAY "Very doubtful"
  WITH CHANCE
    SAY "My reply is no"
  END RANDOM

  ' You may also specify the likelihood of each option
  ' The numbers must total 100
  BEGIN RANDOM
  WITH CHANCE 99
    ' Most of the time, nothing intesting happens
    SAY
  WITH CHANCE 1
    ' Rarely, something mysterious happens
    SLEEP 400
    SAY "The text slowly changes from blue to red";
    SLEEP 400
    SAY ".";
    SLEEP 400
    SAY ".";
    SLEEP 400
    SAY ".";
    SLEEP 400
    COLOR 4
    SAY " BEWARE!!!"
    COLOR 7
    SAY
  END RANDOM
END WHILE
